
&НаКлиенте
Процедура ДеревоСобытийПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ОбновитьСписокОбработчиков", 0.3, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокОбработчиков()
	
	ТекущиеДанные = Элементы.ДеревоСобытий.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаДерева = Новый Структура("Событие, ТипИсточникаИлиТочкаВозникновения");
	
	ТекущиеДанные.Свойство("Событие", СтрокаДерева.Событие);
	ТекущиеДанные.Свойство("ТипИсточникаИлиТочкаВозникновения", СтрокаДерева.ТипИсточникаИлиТочкаВозникновения);
	
	Элементы.Событие.Видимость								= НЕ ЗначениеЗаполнено(СтрокаДерева.Событие);
	Элементы.ТипИсточникаИлиТочкаВозникновения.Видимость	= НЕ ЗначениеЗаполнено(СтрокаДерева.ТипИсточникаИлиТочкаВозникновения);
	
	ОбновитьСписокОбработчиковНаСервере(СтрокаДерева);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДеревоСобытийНаСервере()
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	                      |	События.Ссылка КАК Событие,
	                      |	НазначенныеОбработчики.ТипИсточникаИлиТочкаВозникновения КАК ТипИсточникаИлиТочкаВозникновения,
	                      |	"""" КАК Представление
	                      |ИЗ
	                      |	Справочник.ктв_сб_События КАК События
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ктв_сб_СобытияНазначенныеОбработчики КАК НазначенныеОбработчики
	                      |		ПО События.Ссылка = НазначенныеОбработчики.Событие
	                      |ГДЕ
	                      |	ВЫБОР
	                      |			КОГДА &Событие = НЕОПРЕДЕЛЕНО
	                      |				ТОГДА ИСТИНА
	                      |			ИНАЧЕ События.Ссылка = &Событие
	                      |		КОНЕЦ
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	Событие,
	                      |	ТипИсточникаИлиТочкаВозникновения
	                      |ИТОГИ ПО
	                      |	Событие");
	Если Параметры.Отбор.Свойство("Событие") Тогда
		Запрос.УстановитьПараметр("Событие", Параметры.Отбор.Событие);
	Иначе						  
		Запрос.УстановитьПараметр("Событие", Неопределено);
	КонецЕсли; 
	Результат = Запрос.Выполнить();
	
	ВремДеревоСобытий = РеквизитФормыВЗначение("ДеревоСобытий");
	ВремДеревоСобытий.Строки.Очистить();
	
	ВыборкаСобытий = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаСобытий.Следующий() Цикл
		
		НоваяСтрокаСобытия = ВремДеревоСобытий.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаСобытия, ВыборкаСобытий);
		НоваяСтрокаСобытия.Представление = СокрЛП(ВыборкаСобытий.Событие);
		
		Выборка = ВыборкаСобытий.Выбрать();
	
		Пока Выборка.Следующий() Цикл
			
			НоваяСтрока = НоваяСтрокаСобытия.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.Представление = СокрЛП(Выборка.ТипИсточникаИлиТочкаВозникновения);
		
		КонецЦикла;
	
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ВремДеревоСобытий, "ДеревоСобытий");
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокОбработчиковНаСервере(СтрокаДерева)
	
	Список.Параметры.УстановитьЗначениеПараметра("Событие"							, СтрокаДерева.Событие);
	Список.Параметры.УстановитьЗначениеПараметра("ТипИсточникаИлиТочкаВозникновения", СтрокаДерева.ТипИсточникаИлиТочкаВозникновения);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновитьДеревоСобытийНаСервере();
	
	Список.Параметры.УстановитьЗначениеПараметра("Событие"							, Неопределено);
	Список.Параметры.УстановитьЗначениеПараметра("ТипИсточникаИлиТочкаВозникновения", Неопределено);
	
КонецПроцедуры
