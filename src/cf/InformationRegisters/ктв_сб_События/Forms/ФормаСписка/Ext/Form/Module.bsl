
&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ОбновитьИнформациюОбОбработкеСобытия", 0.3, Истина);
	
КонецПроцедуры //СписокПриАктивизацииСтроки()

&НаКлиенте
Процедура ОбновитьИнформациюОбОбработкеСобытия()
	
	ТочкаВозникновения = Неопределено;
	Ид = "";
	Ошибка = Ложь;
	ТекстОшибки = "";
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если НЕ ТекущиеДанные = Неопределено Тогда
		ТочкаВозникновения = ТекущиеДанные.ТочкаВозникновения;
		Ид = ТекущиеДанные.Ид;
		Ошибка = ТекущиеДанные.Ошибка;
		ТекстОшибки = ТекущиеДанные.ТекстОшибки;
	КонецЕсли;
	
	ОбновитьИнформациюОбОбработкеСобытияСервер(ТочкаВозникновения, Ид);
	
	Элементы.ГруппаОбработкаОшибка.Видимость = Ошибка;
	
КонецПроцедуры //ОбновитьИнформациюОбОбработкеСобытия()

&НаСервере
Процедура ОбновитьИнформациюОбОбработкеСобытияСервер(ТочкаВозникновения, Ид)
	
	Обработка.Параметры.УстановитьЗначениеПараметра("ТочкаВозникновения", ТочкаВозникновения);
	Обработка.Параметры.УстановитьЗначениеПараметра("Ид"				, Ид);
	
	ОбработкаКлиент.Параметры.УстановитьЗначениеПараметра("ТочкаВозникновения"	, ТочкаВозникновения);
	ОбработкаКлиент.Параметры.УстановитьЗначениеПараметра("Ид"					, Ид);
	
КонецПроцедуры //ОбновитьИнформациюОбОбработкеСобытияСервер()

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекДатаУ = ТекущаяУниверсальнаяДата();
	ТекДата = МестноеВремя(ТекДатаУ, ЧасовойПоясСеанса());
	
	СмещениеВремени = Цел((ТекДата - ТекДатаУ) / 60);
	
	Список.Параметры.УстановитьЗначениеПараметра("СмещениеВремени", СмещениеВремени);

	Обработка.Параметры.УстановитьЗначениеПараметра("ТочкаВозникновения", Справочники.ктв_сб_СобытияТочкиВозникновения.ЭтаИБ);
	Обработка.Параметры.УстановитьЗначениеПараметра("Ид"				, "");
	
	ОбработкаКлиент.Параметры.УстановитьЗначениеПараметра("ТочкаВозникновения"	, Справочники.ктв_сб_СобытияТочкиВозникновения.ЭтаИБ);
	ОбработкаКлиент.Параметры.УстановитьЗначениеПараметра("Ид"					, "");
	
КонецПроцедуры //ПриСозданииНаСервере()
