
// Функция - Обработчик запроса Get HTTP-сервиса EventsRegistered
//			возвращает список зарегистрированных событий
//
// Параметры:
//  Запрос	 - HTTP-запрос	 - Полученный HTTP-Запрос
// 
// Возвращаемое значение:
//  HTTP-ответ - Ответ сервиса, содержащий результат выполнения запроса
//
Функция RegisteredEventsGet(Запрос)
	
	МассивСобытий = ктв_сб_СобытияОбработкаВнешнихЗапросов.ПолучитьСписокЗарегистрированныхСобытий();
	
	ЗаписьДанныхОтвета = Новый ЗаписьJSON();
	ЗаписьДанныхОтвета.УстановитьСтроку();
	
	НастройкиСериализации = Новый НастройкиСериализацииJSON();
	НастройкиСериализации.ВариантЗаписиДаты = ВариантЗаписиДатыJSON.УниверсальнаяДата;
	НастройкиСериализации.ФорматСериализацииДаты = ФорматДатыJSON.ISO;
	
	ЗаписатьJSON(ЗаписьДанныхОтвета, МассивСобытий, НастройкиСериализации);
	
	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.Заголовки.Вставить("Content-Type", "application/json; charset=utf-8");
	Ответ.УстановитьТелоИзСтроки(ЗаписьДанныхОтвета.Закрыть());
	
	Возврат Ответ;
	
КонецФункции //RegisteredEventsGet()

// Функция - Обработчик запроса POST HTTP-сервиса Event
//			регистрирует переданные события и запускает обработку событий
//
// Параметры:
//  Запрос	 - HTTP-запрос	 - Полученный HTTP-Запрос
// 
// Возвращаемое значение:
//  HTTP-ответ - Ответ сервиса, содержащий результат выполнения запроса
//
Функция EventsRise(Запрос)
	
	ТекстЗапроса = Запрос.ПолучитьТелоКакСтроку();
	
	ЧтениеДанныхЗапроса = Новый ЧтениеJSON();
	ЧтениеДанныхЗапроса.УстановитьСтроку(ТекстЗапроса);
	
	СвойстваСоЗначениемДата = Новый Массив();
	СвойстваСоЗначениемДата.Добавить("Дата");
	
	Попытка
		ДанныеЗапроса = ПрочитатьJSON(ЧтениеДанныхЗапроса, Ложь, СвойстваСоЗначениемДата, ФорматДатыJSON.ISO);
	Исключение
		Ответ = Новый HTTPСервисОтвет(400, "Некорректно указано описание событий!");
		Ответ.Заголовки.Вставить("Content-Type", "application/html; charset=utf-8");
		Возврат Ответ;
	КонецПопытки;
	
	Если НЕ ТипЗнч(ДанныеЗапроса) = Тип("Массив") Тогда
		Ответ = Новый HTTPСервисОтвет(400, "Описание событий должно иметь тип ""Массив""!");
		Ответ.Заголовки.Вставить("Content-Type", "application/html; charset=utf-8");
		Возврат Ответ;
	КонецЕсли;
	
	ОписаниеОшибок = "";
	
	РезультатРегистрации = ктв_сб_СобытияОбработкаВнешнихЗапросов.ЗарегистрироватьСобытия(ДанныеЗапроса, ОписаниеОшибок);
	
	Если НЕ РезультатРегистрации Тогда
		Ответ = Новый HTTPСервисОтвет(400, ОписаниеОшибок);
		Ответ.Заголовки.Вставить("Content-Type", "application/html; charset=utf-8");
		Возврат Ответ;
	КонецЕсли;
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	Возврат Ответ;
	
КонецФункции //EventsRise()

// Функция - Обработчик запроса Get HTTP-сервиса EventPoints
//			возвращает список зарегистрированных точек возникновения событий
//
// Параметры:
//  Запрос	 - HTTP-запрос	 - Полученный HTTP-Запрос
// 
// Возвращаемое значение:
//  HTTP-ответ - Ответ сервиса, содержащий результат выполнения запроса
//
Функция EventPointsGet(Запрос)
	
	МассивТочек = ктв_сб_СобытияОбработкаВнешнихЗапросов.ПолучитьСписокТочекВозникновенияСобытий();
	
	ЗаписьДанныхОтвета = Новый ЗаписьJSON();
	ЗаписьДанныхОтвета.УстановитьСтроку();
	
	НастройкиСериализации = Новый НастройкиСериализацииJSON();
	НастройкиСериализации.ВариантЗаписиДаты = ВариантЗаписиДатыJSON.УниверсальнаяДата;
	НастройкиСериализации.ФорматСериализацииДаты = ФорматДатыJSON.ISO;
	
	ЗаписатьJSON(ЗаписьДанныхОтвета, МассивТочек, НастройкиСериализации);
	
	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.Заголовки.Вставить("Content-Type", "application/json; charset=utf-8");
	Ответ.УстановитьТелоИзСтроки(ЗаписьДанныхОтвета.Закрыть());
	
	Возврат Ответ;
	
КонецФункции //EventPointsGet()

// Функция - Обработчик запроса Get HTTP-сервиса ThisEventPoint
//			возвращает описание точки возникновения событий, соответствующее текущей ИБ
//
// Параметры:
//  Запрос	 - HTTP-запрос	 - Полученный HTTP-Запрос
// 
// Возвращаемое значение:
//  HTTP-ответ - Ответ сервиса, содержащий результат выполнения запроса
//
Функция ThisEventPointGet(Запрос)
	
	МассивТочек = ктв_сб_СобытияОбработкаВнешнихЗапросов.ПолучитьСписокТочекВозникновенияСобытий(Истина);
	
	ЗаписьДанныхОтвета = Новый ЗаписьJSON();
	ЗаписьДанныхОтвета.УстановитьСтроку();
	
	НастройкиСериализации = Новый НастройкиСериализацииJSON();
	НастройкиСериализации.ВариантЗаписиДаты = ВариантЗаписиДатыJSON.УниверсальнаяДата;
	НастройкиСериализации.ФорматСериализацииДаты = ФорматДатыJSON.ISO;
	
	ЗаписатьJSON(ЗаписьДанныхОтвета, МассивТочек, НастройкиСериализации);
	
	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.Заголовки.Вставить("Content-Type", "application/json; charset=utf-8");
	Ответ.УстановитьТелоИзСтроки(ЗаписьДанныхОтвета.Закрыть());
	
	Возврат Ответ;
	
КонецФункции //ThisEventPointGet()
